#version 460

#define GPU_WARP_SIZE 32
#define GPU_GROUP_SIZE GPU_WARP_SIZE
#define GPU_PRIMITIVES_PER_MESHLET GPU_GROUP_SIZE

// Number of threads per workgroup
// - always 1D
// - max is warpsize = 32
layout(local_size_x=GPU_PRIMITIVES_PER_MESHLET, local_size_y=1, local_size_z=1) in;

#define GPU_MESHLETS_PER_INVOCATION 3
#define GPU_MESHLETS_PER_WORKGROUP (GPU_GROUP_SIZE * GPU_MESHLETS_PER_INVOCATION)

#define GPU_INDICES_PER_PRIMITIVE 3
#define GPU_VERTICES_PER_MESHLET  GPU_PRIMITIVES_PER_MESHLET * GPU_INDICES_PER_PRIMITIVE

#define GPU_INDEX_BUFFER_BINDING 0
#define GPU_MESHLET_DESCRIPTOR_BINDING 1
#define GPU_VISIBLE_INDEX_BUFFER_BINDING 2
#define GPU_DRAW_INDIRECT_BUFFER_BINDING 3

const float pi_half = 1.57079;

struct BoundingSphere
{
  vec3 center;
  float radius;
};

struct BoundingCone
{
  vec3 normal;
  float angle;
};

struct MeshletDescriptor
{
  BoundingSphere sphere;
  BoundingCone cone;
  uint primitive_count;
};

struct DrawElementsIndirectCommand{
  uint count;
  uint instanceCount;
  uint firstIndex;
  uint baseVertex;
  uint baseInstance;
};

layout(binding = GPU_INDEX_BUFFER_BINDING, std430) readonly buffer index_ssbo
{
	uint indices[];
};
layout(binding = GPU_MESHLET_DESCRIPTOR_BINDING, std430) readonly buffer meshlet_descriptor_ssbo
{
	MeshletDescriptor meshlet_descriptors[];
};

layout(binding = GPU_VISIBLE_INDEX_BUFFER_BINDING, std430) writeonly buffer visible_index_ssbo
{
	uint indices[];
} visible;

layout(binding = GPU_DRAW_INDIRECT_BUFFER_BINDING, std430) buffer draw_indirect_ssbo
{
	DrawElementsIndirectCommand draw_indirect_commands[];
};

uniform bool disable_frustum_culling;
uniform bool disable_backface_culling;

uniform vec4 frustum_planes[6]; //obj space
uniform vec3 view_origin; //obj space
//uniform vec3 view_up; //obj space
//uniform vec3 view_right; //obj space

uniform float debug_value;

//taskNV out Task
//{
//  uint   primitive_base_id;
//  uint   primitive_offsets[GPU_GROUP_SIZE];
//  uint   meshlet_ids[GPU_GROUP_SIZE];
//} OUT;

bool isFrontFaceVisible(
  vec3 sphere_center, float sphere_radius,
  vec3 cone_normal, float cone_angle)
{
  vec3 sphere_center_dir = normalize(sphere_center - view_origin);

  float sin_cone_angle = sin(min(cone_angle, pi_half)) + debug_value; //TODO: precompute offline
  return dot(cone_normal, sphere_center_dir) < sin_cone_angle;
}

bool isSphereOutsideFrustum(
  vec3 sphere_center, float sphere_radius)
{
  bool isInside = false;
  for(int i = 0; i < 6; i++)
  {
    isInside = isInside || (dot(frustum_planes[i].xyz, sphere_center) + frustum_planes[i].w < -sphere_radius);
  }
  return isInside;
}

// Exact version considering sphere radius 
//bool isFrontFaceVisibleExact(
//  vec3 sphere_center, float sphere_radius,
//  vec3 cone_normal, float cone_angle)
//{
//  vec3 sphere_center_dir = normalize(sphere_center - view_origin);
//
//  vec3 right_offset = sphere_radius * cross(sphere_center, view_up);
//  vec3 up_offset = sphere_radius * cross(sphere_center, view_right);
//
//  vec3 sphere_bound_dirs[4] = vec3[4](
//    normalize((sphere_center - right_offset) - view_origin),
//    normalize((sphere_center + up_offset)    - view_origin),
//    normalize((sphere_center + right_offset) - view_origin),
//    normalize((sphere_center - up_offset)    - view_origin)
//  );
//
//  float angular_span = 0.0;
//  for(int i = 0; i < 4; i++)
//  {
//    float temp = dot(sphere_center_dir, sphere_bound_dirs[i]);
//    temp = temp * temp;
//    temp = sqrt(1 - temp);// = sin(acos(dot(a,b)))
//    angular_span = max(angular_span, temp); 
//  }
//
//  float sin_cone_angle = sin(min(cone_angle, pi_half)) + debug_value; //TODO: precompute offline
//  return (dot(cone_normal, sphere_center_dir) - angular_span) < sin_cone_angle;
//}

shared bool sh_render;
shared uint sh_visible_base_id;

void main()
{
  if (gl_LocalInvocationID.x == 0)
  {
    uint meshlet_id = gl_WorkGroupID.x;
    MeshletDescriptor desc = meshlet_descriptors[meshlet_id];

    bool render = (disable_frustum_culling || !isSphereOutsideFrustum(desc.sphere.center, desc.sphere.radius)) &&
                (disable_backface_culling || isFrontFaceVisible(desc.sphere.center, desc.sphere.radius,
                                   desc.cone.normal, desc.cone.angle));
    //sh_render = sh_render || gl_LocalInvocationID.x == 0;
    if (render)
    {
      sh_visible_base_id = atomicAdd(draw_indirect_commands[0].count, GPU_VERTICES_PER_MESHLET);
    }
    sh_render = render;
  }
//  if (gl_LocalInvocationID.x == 1)
//  {
//    sh_render = true;
//    sh_visible_base_id = atomicAdd(draw_indirect_commands[0].count, GPU_VERTICES_PER_MESHLET);
//  }
  memoryBarrierShared();
  barrier();

  if(sh_render)
  {
    uint primitive_id = gl_GlobalInvocationID.x;
    uint local_primitive_id = gl_LocalInvocationID.x;
    uint index_base_id = primitive_id * GPU_INDICES_PER_PRIMITIVE;
    uint local_index_base_id = local_primitive_id * GPU_INDICES_PER_PRIMITIVE;

    for(uint i = 0; i < GPU_INDICES_PER_PRIMITIVE; i++)
    {
      visible.indices[sh_visible_base_id + local_index_base_id + i] = 
        indices[index_base_id + i];
    }
  }
}
